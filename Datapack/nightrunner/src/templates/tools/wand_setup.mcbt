# Template for wands:
# (Not implemented yet, always entity)hit_entity_or_block: 1 for entity, 2 for block
# Returns score to "@s" "Nightrunner_Item_Ray_Return" base on hit: 1 entity, 2 block, 3 no hit
# function_name: function name for the wand
template wand_setup {
    with data:js{
        # damage @s <%damage%> minecraft:magic by @a[limit=1,tag=raycasting,sort=nearest,nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",tag:{Nightrunner_TestWand:1b}}}]
        function ./check
        return 0
        block start_raycast{
            <%%
                StartRaycastContent = [];
                // Reset Nightrunner Mana
                StartRaycastContent.push('scoreboard players reset $Nightrunner Nightrunner_Mana')

                // Check if cooldown is active
                StartRaycastContent.push('execute if score @s Nightrunner_SpellCooldown matches 1.. run return 1')

                let value = data.ManaCost - 1;

                // Check if the spell can be casted without mana
                let ManaStart = 'execute if score @s[gamemode=!creative] Nightrunner_Mana matches ..';
                if(data.MainSpell.Name == "Damage") // Damage spell can be casted without mana but with damage loss
                {
                    StartRaycastContent.push(ManaStart + value + ' run scoreboard players set $Nightrunner Nightrunner_Weak_Use 1')
                }
                else
                {
                    StartRaycastContent.push(ManaStart + value + ' run return run tellraw @s [{"text":"Not enough mana!","color":"red"}]')
                }
                // Play sound
                if(data.MainSpell.Name == "Damage") // Damage spell can be casted without mana but with damage loss
                {
                    StartRaycastContent.push('execute if score @s[gamemode=!creative] Nightrunner_Mana matches ..' + value + ' run execute at @s run playsound minecraft:entity.illusioner.prepare_mirror master @a ~ ~ ~ 0.3 1.5')
                    StartRaycastContent.push('execute if score @s[gamemode=!creative] Nightrunner_Mana matches ' + data.ManaCost + '.. run execute at @s run playsound minecraft:entity.illusioner.prepare_mirror master @a ~ ~ ~ 0.3 2')
                    StartRaycastContent.push('execute as @s[gamemode=creative] run execute at @s run playsound minecraft:entity.illusioner.prepare_mirror master @a ~ ~ ~ 0.3 2')
                }
                else
                {
                    StartRaycastContent.push('execute at @s run playsound minecraft:entity.illusioner.prepare_mirror master @a ~ ~ ~ 0.3 2')
                }

                // Add Cooldown
                StartRaycastContent.push('scoreboard players set @s Nightrunner_SpellCooldown ' + data.Cooldown)
                // Reset scoreboards
                StartRaycastContent.push('scoreboard players reset @s Nightrunner_Item_Ray_steps')
                StartRaycastContent.push('scoreboard players reset $Nightrunner Nightrunner_Item_Ray_steps')

                // Set spell range
                StartRaycastContent.push(`execute as @s run scoreboard players operation $Nightrunner Nightrunner_RangedSpellRange = @s Nightrunner_RangedSpellRange`);
                let LeatherArmor = ['leather_boots', 'leather_leggings', 'leather_chestplate', 'leather_helmet'];
                let RangeAddAmount = 15;
                for(let i = 0; i < LeatherArmor.length; i++)
                {
                    StartRaycastContent.push('execute as @s if entity @s[nbt={Inventory:[{Slot:' + (100 + i) + 'b,id:"minecraft:' + LeatherArmor[i] + '"}]}] run scoreboard players add $Nightrunner Nightrunner_RangedSpellRange ' + RangeAddAmount);
                }
                // tell spell range
                StartRaycastContent.push(`tellraw @s [{"text":"Spell range: ","color":"aqua"},{"score":{"name":"$Nightrunner","objective":"Nightrunner_RangedSpellRange"}}]`);

                // Reset effect cooldown
                StartRaycastContent.push(`scoreboard players reset $NightrunnerCooldown Nightrunner_EffectCooldown`);

                // Raycast
                StartRaycastContent.push(`tag @s add raycasting`);
                StartRaycastContent.push(`execute anchored eyes positioned ^ ^ ^ store result score @s Nightrunner_Item_Ray_Return run function ./raycast`);
                StartRaycastContent.push(`tag @s remove raycasting`);

                // Reset weak use
                StartRaycastContent.push(`scoreboard players reset $Nightrunner Nightrunner_Weak_Use`);
                emit.mcb(StartRaycastContent.join('\n'));

                // Reduce mana (not in creative mode)
                if(data.ConsumeManaOnCast)
                {
                    let halfMana = data.ManaCost / 2;
                    if(halfMana < 1) halfMana = 0;
                    let TakeHalfMana = `scoreboard players remove @s Nightrunner_Mana ` + halfMana;
                    let blockArray = [];

                    // Mana efficiency 1
                    blockArray.push(`execute as @s[gamemode=!creative,scores={Nightrunner_Mana=1..},nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",count:1,components:{"minecraft:custom_data":{wand:true},"minecraft:enchantments":{levels:{"nightrunner:mana_efficiency":1}}}}}] run execute if predicate nightrunner:` + `10change` + ` run return run ` + TakeHalfMana);

                    // Mana efficiency 2
                    blockArray.push(`execute as @s[gamemode=!creative,scores={Nightrunner_Mana=1..},nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",count:1,components:{"minecraft:custom_data":{wand:true},"minecraft:enchantments":{levels:{"nightrunner:mana_efficiency":2}}}}}] run execute if predicate nightrunner:` + `35change` + ` run return run ` + TakeHalfMana);

                    // Mana efficiency 3
                    blockArray.push(`execute as @s[gamemode=!creative,scores={Nightrunner_Mana=1..},nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",count:1,components:{"minecraft:custom_data":{wand:true},"minecraft:enchantments":{levels:{"nightrunner:mana_efficiency":3}}}}}] run execute if predicate nightrunner:` + `50change` + ` run return run ` + TakeHalfMana);

                    // Default
                    blockArray.push('execute as @s[gamemode=!creative,scores={Nightrunner_Mana=1..}] run scoreboard players remove @s Nightrunner_Mana ' + data.ManaCost)

                    emit.block(blockArray);
                }
                else
                {
                    StartRaycastContent.push('scoreboard players set $Nightrunner Nightrunner_Mana ' + data.ManaCost)
                }
                // Item damage
                let blockArray = [];

                // Unbreaking 1
                blockArray.push(`execute if entity @s[gamemode=!creative,nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",components:{"minecraft:custom_data":{` + data.Namespace + `:true,wand:true},"minecraft:enchantments":{levels:{"minecraft:unbreaking":1}}}}}] run execute if predicate nightrunner:50change run return 1`);
                blockArray.push(`execute if entity @s[gamemode=!creative,nbt={Inventory:[{id:"minecraft:warped_fungus_on_a_stick",Slot:-106b,components:{"minecraft:custom_data":{` + data.Namespace + `:true,wand:true},"minecraft:enchantments":{levels:{"minecraft:unbreaking":1}}}}]}] run execute if predicate nightrunner:50change run return 1`);

                // Unbreaking 2
                blockArray.push(`execute if entity @s[gamemode=!creative,nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",components:{"minecraft:custom_data":{` + data.Namespace + `:true,wand:true},"minecraft:enchantments":{levels:{"minecraft:unbreaking":2}}}}}] run execute if predicate nightrunner:66change run return 1`);
                blockArray.push(`execute if entity @s[gamemode=!creative,nbt={Inventory:[{id:"minecraft:warped_fungus_on_a_stick",Slot:-106b,components:{"minecraft:custom_data":{` + data.Namespace + `:true,wand:true},"minecraft:enchantments":{levels:{"minecraft:unbreaking":2}}}}]}] run execute if predicate nightrunner:66change run return 1`);

                // Unbreaking 3
                blockArray.push(`execute if entity @s[gamemode=!creative,nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",components:{"minecraft:custom_data":{` + data.Namespace + `:true,wand:true},"minecraft:enchantments":{levels:{"minecraft:unbreaking":3}}}}}] run execute if predicate nightrunner:75change run return 1`);
                blockArray.push(`execute if entity @s[gamemode=!creative,nbt={Inventory:[{id:"minecraft:warped_fungus_on_a_stick",Slot:-106b,components:{"minecraft:custom_data":{` + data.Namespace + `:true,wand:true},"minecraft:enchantments":{levels:{"minecraft:unbreaking":3}}}}]}] run execute if predicate nightrunner:75change run return 1`);

                // Default
                blockArray.push(`execute if entity @s[gamemode=!creative,nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",components:{"minecraft:custom_data":{` + data.Namespace + `:true,wand:true}}}}] run item modify entity @s weapon.mainhand nightrunner:damage_item`);
                blockArray.push(`execute if entity @s[gamemode=!creative,nbt={Inventory:[{id:"minecraft:warped_fungus_on_a_stick",Slot:-106b,components:{"minecraft:custom_data":{` + data.Namespace + `:true,wand:true}}}]}] run item modify entity @s weapon.offhand nightrunner:damage_item`);

                emit.block(blockArray);
            %%>
        }
        block raycast{
            <%%
                let CheckQuality = 0.9;
                let ExecuteAsRaycast = 'execute as @e[dx=0,type=!#nightrunner:not_mob,tag=!raycasting,limit=1] positioned ~-';
                let RunNightrunnerWandFunction = ' run function nightrunner:items/tools/' + data.Folder + '/';
                // If hit entity
                if(data.MainSpell.HitType == 1)
                {
                    // Add coordinates
                    ExecuteAsRaycast = ExecuteAsRaycast + CheckQuality +' ~-' + CheckQuality +' ~-' + CheckQuality +' if entity @s[dx=0] positioned ~' + CheckQuality +' ~' + CheckQuality +' ~' + CheckQuality;
                    switch(data.MainSpell.Name)
                    {
                        case "Damage":
                            emit(ExecuteAsRaycast + RunNightrunnerWandFunction + data.FunctionName + '/spell_damage');
                            break;
                        default:
                            emit(ExecuteAsRaycast + RunNightrunnerWandFunction + data.FunctionName + '/hit_entity');
                            break;
                    }
                    // Return if hit entity
                    emit(ExecuteAsRaycast +' run return 1');

                    // If hit enetity but hit block
                    emit('execute unless block ~ ~ ~ #nightrunner:air run particle minecraft:cloud ~ ~ ~ 0 0 0 0.02 1 force');
                }
                else // If hit block
                {
                    emit('execute unless block ~ ~ ~ #nightrunner:air' + RunNightrunnerWandFunction + data.FunctionName + '/hit_block');
                }
                // If no hit
                emit('execute unless block ~ ~ ~ #nightrunner:air run return 2');
            %%>
            #
            execute if score $Nightrunner Nightrunner_Item_Ray_steps >= $Nightrunner Nightrunner_RangedSpellRange run block{
                particle minecraft:cloud ~ ~ ~ 0 0 0 0.02 1 force
            }
            execute if score $Nightrunner Nightrunner_Item_Ray_steps >= $Nightrunner Nightrunner_RangedSpellRange run return 3

            # particle effect
            execute if score $Nightrunner Nightrunner_Item_Ray_steps matches 15.. run block effect{
                # cooldown
                execute if score $NightrunnerCooldown Nightrunner_EffectCooldown matches 1.. run scoreboard players remove $NightrunnerCooldown Nightrunner_EffectCooldown 1
                execute if score $NightrunnerCooldown Nightrunner_EffectCooldown matches 1.. run return 1
                particle <%data.Particle%> ~ ~ ~ 0 0 0 0.02 1 force
                scoreboard players add $NightrunnerCooldown Nightrunner_EffectCooldown 8
            }

            # Update steps
            scoreboard players add $Nightrunner Nightrunner_Item_Ray_steps 1
            # Loop
            execute positioned ^ ^ ^0.25 rotated ~ ~ run function ./raycast
        }
        block give{
            give @s minecraft:warped_fungus_on_a_stick[custom_data={<%data.Namespace%>:true,wand:true},item_name='{"text":"<% data.Name %>","italic":false,"color":"<%data.Color%>"}',lore=[<%data.Description%>,'{"text":"Nightrunner","color":"aqua"}']<%data.Attributes.GiveLiteral%>,custom_model_data=<%data.CustomModelData%>,max_damage=<%data.Durability%>,damage=0.5] 1
        }
        block check{
            execute as @a if score @s Nightrunner_Used_Magic_tool matches 1 run block check_wand{
                # Selected item
                <%%
                    if(data.MainSpell && data.MainSpell.Name !== undefined) // Main item
                    {
                        let MainFunctionName = 'function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/use';
                        switch(data.MainSpell.Name)
                        {
                            case "Damage":
                                MainFunctionName = 'execute at @s run function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + "/start_raycast";
                                break;
                            case "Heal":
                                MainFunctionName = 'function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/spell_heal';
                                break;
                            case "Raycast":
                                MainFunctionName = 'execute at @s run function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/start_raycast';
                                break;
                        }
                        // Emit function
                        emit('execute if entity @s[nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",components:{"minecraft:custom_data":{' + data.Namespace + ':true,wand:true}}}}] run ' + MainFunctionName);
                    }

                    if(data.SecondarySpell && data.SecondarySpell.Name !== undefined) // Offhand item
                    {
                        emit('# Offhand item');
                        let OffhandFunctionName = 'function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/use_secondary';
                        switch(data.SecondarySpell.Name)
                        {
                            case "Damage":
                                OffhandFunctionName = 'execute at @s run function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + "/start_raycast";
                                break;
                            case "Heal":
                                OffhandFunctionName = 'function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/spell_heal';
                                break;
                            case "Raycast":
                                OffhandFunctionName = 'execute at @s run function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/start_raycast';
                                break;
                        }
                        // Emit function
                        emit('execute if entity @s[nbt={Inventory:[{id:"minecraft:warped_fungus_on_a_stick",Slot:-106b,components:{"minecraft:custom_data":{' + data.Namespace + ':true,wand:true}}}]}] run ' + OffhandFunctionName);
                    }
                %%>
                # Reset
                execute if entity @s[nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",components:{"minecraft:custom_data":{<%data.Namespace%>:true,wand:true}}}}] run scoreboard players reset @s Nightrunner_Used_Magic_tool
                # Reset
                execute if entity @s[nbt={Inventory:[{id:"minecraft:warped_fungus_on_a_stick",Slot:-106b,components:{"minecraft:custom_data":{<%data.Namespace%>:true,wand:true}}}]}] run scoreboard players reset @s Nightrunner_Used_Magic_tool
            }
        }
        block spell_damage{
            # Apply damage if the player has the wand in their hand
            execute if entity @a[limit=1,tag=raycasting,sort=nearest] run block apply_spell_damage{
                # check if entitiy is iron golem
                execute if entity @s[type=minecraft:iron_golem] run return run block{
                    damage @a[limit=1,tag=raycasting,sort=nearest] 2 minecraft:magic by @s
                    playsound minecraft:item.trident.riptide_2 player @a ~ ~ ~ 0.4 0.4
                }

                # If no mana was used, reduce damage
                execute if score $Nightrunner Nightrunner_Weak_Use matches 1 run return run block{
                    say Weak spell
                    damage @s <%data.MainSpell.SpellDamage / 2%> minecraft:magic by @a[limit=1,tag=raycasting,sort=nearest]
                    playsound minecraft:entity.arrow.hit player @a ~ ~ ~ 0.4 0.1
                    effect give @s slowness 1 1 true
                }
                # Deal damage
                damage @s <%data.MainSpell.SpellDamage%> minecraft:magic by @a[limit=1,tag=raycasting,sort=nearest]
                playsound minecraft:entity.arrow.hit player @a ~ ~ ~ 0.4 0.4
                effect give @s slowness 1 1 true
            }
        }
        block spell_heal{
            effect give @s minecraft:instant_health 1 <%data.HealAmount%>
        }
    }
}