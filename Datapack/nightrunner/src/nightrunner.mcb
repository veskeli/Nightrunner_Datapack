import ./templates/messages.mcbt
import ./templates/scoreboard/settings_scoreboard.mcbt
import ./templates/settings/settings_page.mcbt
import ./templates/log.mcbt
# Nightrunner
function on_load minecraft:load{
    #=================================================
    # Load all scoreboards
    #=================================================

    #================= internal scoreboard
    scoreboard objectives add nightrunner.internal dummy

    #================= Settings
    scoreboard objectives add Nightrunner_BlockMobs dummy
    # Operators
    scoreboard objectives add Nightrunner_Op dummy
    # open settings trigger
    scoreboard objectives add Open_Nightrunner_Settings trigger

    #================= Join
    scoreboard objectives add Nightrunner_Joined dummy
    scoreboard objectives add Nightrunner_ID dummy

    #=================================================
    # Gamerules
    #=================================================
    gamerule commandBlockOutput false
    gamerule playersSleepingPercentage 50
    gamerule doInsomnia false

    #=================================================
    # Show the welcome message
    #=================================================
    # Show the welcome message (Takes build number [int] and update message [raw])
    welcome 2 More dev tools
}
#=================================================
# MARK: Settings
#=================================================
dir settings{
    function settings{
        settings_page 1
    }
    clock join_check 1s{
        # Check if player doesn't have a profile
        execute as @a run execute unless score @s Nightrunner_Joined matches 1 run block join_setup{
            # Set the player as joined
            scoreboard players set @s Nightrunner_Joined 1
            # +1 to the internal id
            scoreboard players add $Nightrunner_Internal_ID Nightrunner_ID 1
            # Set the player id
            scoreboard players operation @s Nightrunner_ID = $Nightrunner_Internal_ID Nightrunner_ID
        }
    }
    function reset_join{
        # Reset the joined status
        scoreboard players reset @a Nightrunner_Joined
        scoreboard players reset @a Nightrunner_ID
        # Reset the internal id
        scoreboard players reset $Nightrunner_Internal_ID Nightrunner_ID
    }
    function tick minecraft:tick{
        # Allow the player to open the settings if they are op
        execute as @a[scores={Nightrunner_Op=1}] run scoreboard players enable @s Open_Nightrunner_Settings

        # Check if the player ran the settings command
        execute as @a[scores={Open_Nightrunner_Settings=1}] run block open_settings{
            scoreboard players reset @s Open_Nightrunner_Settings
            function nightrunner:settings/settings
        }
    }
    dir toggles{
        function toggle_health_system{
            # Toggle the health system
            execute if score $Nightrunner Nightrunner_UseReviveSystem matches 1 run {
                scoreboard players set $Nightrunner Nightrunner_UseReviveSystem 0
                # open settings again
                function ../settings
            } else run {
                scoreboard players set $Nightrunner Nightrunner_UseReviveSystem 1
                # open settings again
                function ../settings
            }
        }
    }
}
#=================================================
# MARK: Health system
#=================================================
dir health_system{
    function load minecraft:load{
        #=================================================
        # Scoreboards
        #=================================================
        # Use Revive system
        scoreboard objectives add Nightrunner_UseReviveSystem dummy
        # Total Death count
        scoreboard objectives add Nightrunner_DeathCount deathCount
        # This is used to check if player died (And gets reset every time the player dies)
        scoreboard objectives add Nightrunner_Death deathCount
        # Is player dead
        scoreboard objectives add Nightrunner_CurrentlyDead dummy
        # Op Revive me trigger
        scoreboard objectives add Nightrunner_Revive trigger
        # Op Revive all trigger
        scoreboard objectives add Nightrunner_ReviveAll trigger

        #=================================================
        # Load the health system
        #=================================================
        # Set immediate respawn gamerule
        gamerule doImmediateRespawn false

        #=================================================
        # Check scoreboard settings
        #=================================================
        execute unless score $Nightrunner Nightrunner_UseReviveSystem matches 9999.. run scoreboard players set $Nightrunner Nightrunner_UseReviveSystem 1
    }
    function tick minecraft:tick{
        # TODO: Dont use tick and use schedule instead
        # Check if the revive system is enabled
        execute if score $Nightrunner Nightrunner_UseReviveSystem matches 0 run return 1
        # Check if the player is dead
        execute as @a[scores={Nightrunner_Death=..1}] run block death_screen{
            # execute the death function
            function ./death
            # Reset the dead status
            scoreboard players reset @s Nightrunner_Death
        }
    }
    function death{
        # if player is not in survival mode then return
        execute as @s[nbt={playerGameType:1}] run return run block player_is_in_creative{
            say Player is in creative mode
        }
        # Set player respawn location to the death location
        execute at @s run spawnpoint @s ~ ~ ~
        # Set the player as dead
        scoreboard players set @s Nightrunner_CurrentlyDead 1
        # Set player to spectator
        gamemode spectator @s
        # Show title with death message
        title @s title [{"text":"You died","color":"red"}]
    }
    dir revive{
        function tick minecraft:tick{
            # Give op the ability to revive themselves and others
            execute as @a[scores={Nightrunner_Op=1}] run scoreboard players enable @s Nightrunner_Revive
            execute as @a[scores={Nightrunner_Op=1}] run scoreboard players enable @s Nightrunner_ReviveAll

            # Check if the player ran the revive command
            execute as @a[scores={Nightrunner_Revive=1}] run block trigger_revive{
                # Reset the revive status
                scoreboard players reset @s Nightrunner_Revive
                # execute the revive function
                function ./op_revive
            }
            # Check if the player ran the revive all command
            execute as @a[scores={Nightrunner_ReviveAll=1}] run block trigger_revive_all{
                # Reset the revive all status
                scoreboard players reset @s Nightrunner_ReviveAll
                # execute the revive all function
                function ./op_revive_all
            }
        }
        function op_revive{
            # set the player as not dead
            scoreboard players reset @s Nightrunner_CurrentlyDead
            # Set player to survival
            gamemode survival @s
            # Show title with revive message
            title @s title [{"text":"You have been revived","color":"green"}]
        }
        function op_revive_all{
            # Execute as dead players
            execute as @a[scores={Nightrunner_CurrentlyDead=1}] run function ./op_revive
        }
    }
}
#=================================================
# MARK: Mobs
#=================================================
dir mobs{
    #=================================================
    # Monsters
    dir monsters{
        #=================================================
        # Zombie
        function zombie minecraft:tick{
            execute if predicate nightrunner:10change run execute as @e[type=zombie,tag=!Nightrunner,limit=1] run data merge entity @s {CanBreakDoors:1b,Tags:["Nightrunner"],attributes:[{id:"minecraft:generic.attack_damage",base:10},{id:"minecraft:generic.movement_speed",base:0.31},{id:"minecraft:zombie.spawn_reinforcements",base:0.125}]}

            execute as @e[type=zombie,tag=!Nightrunner,limit=1] run data merge entity @s {CustomName:'"test"',CanBreakDoors:1b,Tags:["Nightrunner"],attributes:[{id:"minecraft:generic.attack_damage",base:10},{id:"minecraft:generic.movement_speed",base:0.31},{id:"minecraft:zombie.spawn_reinforcements",base:0.125}]}
            # Speedboy
            # execute if predicate nightmare:10change run data_speedboy zombie 5 0.55 Quickstep Zombie
            # Strongboy
            # execute if predicate nightmare:10change run data_normal_named zombie 12 0.23 Sturdy Zombie
            # Vanilla
            # data_damage_speed zombie 9 0.31
            # execute as @e[type=<%entity_type%>,tag=!Nightrunner,limit=1] run data merge entity @s {Attributes:[{Base:<%attack_damage%>,Name:"generic.attack_damage"},{Base:<%move_speed%>,Name:"generic.movement_speed"}], CanPickUpLoot:1b,Tags:["nightmare"], CustomName:'{"text":"<%entity_name%>"}',CustomNameVisible:0b}
        }
        function drowned minecraft:tick{
           execute as @e[type=drowned,tag=!Nightrunner,limit=1] run data merge entity @s {CanBreakDoors:1b,Tags:["Nightrunner"],attributes:[{id:"minecraft:generic.attack_damage",base:10},{id:"minecraft:generic.movement_speed",base:0.31},{id:"minecraft:zombie.spawn_reinforcements",base:0.125}]}


        }
        function husk minecraft:tick{
            execute as @e[type=husk,tag=!Nightrunner,limit=1] run data merge entity @s {CanBreakDoors:1b,Tags:["Nightrunner"],attributes:[{id:"minecraft:generic.attack_damage",base:10},{id:"minecraft:generic.movement_speed",base:0.31},{id:"minecraft:zombie.spawn_reinforcements",base:0.125}]}


        }
        function zombie_villager minecraft:tick{
            execute as @e[type=zombie_villager,tag=!Nightrunner,limit=1] run data merge entity @s {CanBreakDoors:1b,Tags:["Nightrunner"],attributes:[{id:"minecraft:generic.attack_damage",base:10},{id:"minecraft:generic.movement_speed",base:0.31},{id:"minecraft:zombie.spawn_reinforcements",base:0.125}]}


        }
        function skeleton minecraft:tick{
            # execute as @e[type=husk,tag=!Nightrunner,limit=1] run data merge entity @s {CanBreakDoors:1b,Tags:["Nightrunner"],attributes:[{id:"minecraft:generic.attack_damage",base:10},,{id:"minecraft:generic.movement_speed",base:0.31},{id:"minecraft:zombie.spawn_reinforcements",base:0.125}]}
        }
        function stray minecraft:tick{
            execute as @e[type=stray,tag=!Nightrunner,limit=1] run block data/stray{
                # Store a random value
                execute store result score @s nightrunner.internal run random value 1..100
                # Set the skeleton data based on the random value
                execute if score @s[tag=!Nightrunner] nightrunner.internal matches ..100 run data merge entity @s {CustomName:'"test"',Tags:["Nightrunner"],attributes:[{id:"minecraft:generic.attack_damage",base:10},{id:"minecraft:generic.movement_speed",base:0.31}]}
            }

        }
        function bogged minecraft:tick{
            execute as @e[type=bogged,tag=!Nightrunner,limit=1] run block data/bogged{
                # Store a random value
                execute store result score @s nightrunner.internal run random value 1..100
                # Set the skeleton data based on the random value
                execute if score @s[tag=!Nightrunner] nightrunner.internal matches ..100 run data merge entity @s {CustomName:'"test"',Tags:["Nightrunner"],attributes:[{id:"minecraft:generic.attack_damage",base:10},{id:"minecraft:generic.movement_speed",base:0.31}]}
            }

        }
        function creeper minecraft:tick{
            execute as @e[type=creeper,tag=!Nightrunner,limit=1] run block data/creeper{
                # Store a random value
                execute store result score @s nightrunner.internal run random value 1..100
                # Set the skeleton data based on the random value
                execute if score @s[tag=!Nightrunner] nightrunner.internal matches ..5 run data merge entity @s {CustomName:'"Nuke"',Tags:["Nightrunner"],ExplosionRadius:15b,Fuse:45,attributes:[{id:"minecraft:generic.movement_speed",base:0.20},{id:"minecraft:generic.max_health", base:30},{id:"minecraft:generic.scale",base:1.25}]}

                execute if score @s[tag=!Nightrunner] nightrunner.internal matches ..15 run data merge entity @s {CustomName:'"Lite"',Tags:["Nightrunner"],ExplosionRadius:2b,Fuse:15,attributes:[{id:"minecraft:generic.movement_speed",base:0.3},{id:"minecraft:generic.max_health", base:10},{id:"minecraft:generic.scale",base:0.75}]}
                
                execute if score @s[tag=!Nightrunner] nightrunner.internal matches ..100 run data merge entity @s {CustomName:'"Normi"',Tags:["Nightrunner"],ExplosionRadius:5b,Fuse:30,attributes:[{id:"minecraft:generic.movement_speed",base:0.25},{id:"minecraft:generic.max_health", base:20},{id:"minecraft:generic.scale",base:1}]}
                
            }
            
            # Fuse | explosion | movement speed
            # Volatile (nuke)
            # execute if predicate nightmare:10change run data_normal_creeper 35 8 0.2 Volatile Creeper
            # Quickfuse (insta)
            # execute if predicate nightmare:10change run data_normal_creeper 15 4 2 Quickfuse Creeper
            # Creeping
            # execute if predicate nightmare:10change run data_normal_creeper 35 3 0.3 Creeping Creeper
            # Vanilla
            # data merge entity @e[type=creeper,tag=!nightmare,limit=1] {ExplosionRadius:5,Tags:["nightmare"]}
        }

    }
}
#=================================================
# MARK: Crafting
#=================================================

#=================================================
# MARK: Advacements
#=================================================

#=================================================
# MARK: Predicates
#=================================================
predicate 10change{
  "condition": "minecraft:random_chance",
  "chance": 0.1
}