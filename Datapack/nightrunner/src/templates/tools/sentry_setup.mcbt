template sentry_setup{
    with data:js{
        schedule function ./check_sentry 20t
        schedule function ./sentry_effects 10t
        return 0
        block hit_block{
            # Kill other sentries with the same ID
            execute as @e[type=block_display,tag=sentry] if score @s Nightrunner_SentryID = @a[limit=1,sort=nearest,tag=raycasting] Nightrunner_ID run kill @s

            # summon sentry
            summon block_display ~ ~0.5 ~ {Tags:["sentry","just_spawned","<%data.Namespace%>"],transformation:{left_rotation:[0f,0f,0f,1f],right_rotation:[0f,0f,0f,1f],translation:[-0.35f,0f,-0.35f],scale:[0.7f,0.7f,0.7f]},block_state:{Name:"<%data.DisplayBlock%>"}}
            # find just spawned sentry
            execute as @e[type=block_display,tag=sentry,tag=just_spawned] run block{
                execute at @s run particle ominous_spawning ~ ~ ~ 0 0 0 1 100 normal
                tag @s remove just_spawned

                execute store result score @s Nightrunner_SentryID run scoreboard players get @a[limit=1,sort=nearest,tag=raycasting] Nightrunner_ID
                execute as @a[limit=1,sort=nearest,tag=raycasting,gamemode=!creative] run scoreboard players operation @s Nightrunner_Mana -= $Nightrunner Nightrunner_Mana

                # Reset Nightrunner Mana
                scoreboard players reset $Nightrunner Nightrunner_Mana

                scoreboard players set @s Nightrunner_SentryLifetime <%data.Lifetime%>
                execute at @s run playsound minecraft:item.chorus_fruit.teleport master @a ~ ~ ~ 0.7 0.2
            }
        }
        block check_sentry{
            execute as @e[type=block_display,tag=sentry,tag=<%data.Namespace%>] run block check_current_sentry{
                execute at @s run execute as @e[distance=..<%data.Distance%>,type=<%data.Target%>,limit=<%data.ConcurrentAttacks%>,sort=nearest] run function ./sentry_action
            }
            # Loop
            schedule function ./check_sentry 20t
        }
        block sentry_effects{
            execute as @e[type=block_display,tag=sentry,tag=<%data.Namespace%>] run block{
                execute as @s at @s run particle dust{color:[<%data.DustColor%>],scale:2} ^<%data.Distance%> ^ ^0 0 0 0 0 1 normal
                execute as @s at @s run particle dust{color:[<%data.DustColor%>],scale:2} ^<%data.Distance * 0.707%> ^ ^<%data.Distance * 0.707%> 0 0 0 0 1 normal
                execute as @s at @s run particle dust{color:[<%data.DustColor%>],scale:2} ^0 ^ ^<%data.Distance%> 0 0 0 0 1 normal
                execute as @s at @s run particle dust{color:[<%data.DustColor%>],scale:2} ^-<%data.Distance * 0.707%> ^ ^<%data.Distance * 0.707%> 0 0 0 0 1 normal
                execute as @s at @s run particle dust{color:[<%data.DustColor%>],scale:2} ^-<%data.Distance%> ^ ^0 0 0 0 0 1 normal
                execute as @s at @s run particle dust{color:[<%data.DustColor%>],scale:2} ^-<%data.Distance * 0.707%> ^ ^-<%data.Distance * 0.707%> 0 0 0 0 1 normal
                execute as @s at @s run particle dust{color:[<%data.DustColor%>],scale:2} ^0 ^ ^-<%data.Distance%> 0 0 0 0 1 normal
                execute as @s at @s run particle dust{color:[<%data.DustColor%>],scale:2} ^<%data.Distance * 0.707%> ^ ^-<%data.Distance * 0.707%> 0 0 0 0 1 normal
            }
            schedule function ./sentry_effects 10t
        }
    }
}