# Template for wands:
# (Not implemented yet, always entity)hit_entity_or_block: 1 for entity, 2 for block
# Returns score to "@s" "Nightrunner_Item_Ray_Return" base on hit: 1 entity, 2 block, 3 no hit
# function_name: function name for the wand
template wand_setup {
    with data:js{
        # damage @s <%damage%> minecraft:magic by @a[limit=1,tag=raycasting,sort=nearest,nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",tag:{Nightrunner_TestWand:1b}}}]
        function ./check
        return 0
        block start_raycast{
            # check if cooldown is active
            execute if score @s Nightrunner_SpellCooldown matches 1.. run return 1
            # add cooldown
            scoreboard players set @s Nightrunner_SpellCooldown <%data.Cooldown%>
            # Reset scoreboards
            scoreboard players reset @s Nightrunner_Item_Ray_steps
            scoreboard players reset $Nightrunner Nightrunner_Item_Ray_steps
            execute as @s run scoreboard players operation $Nightrunner Nightrunner_RangedSpellRange = @s Nightrunner_RangedSpellRange
            scoreboard players reset $NightrunnerCooldown Nightrunner_EffectCooldown

            # play sound
            execute at @s run playsound minecraft:entity.illusioner.prepare_mirror master @a ~ ~ ~ 0.3 2

            # Raycast
            tag @s add raycasting
            execute anchored eyes positioned ^ ^ ^ store result score @s Nightrunner_Item_Ray_Return run function ./raycast
            tag @s remove raycasting

            # Returns score to "@s" "Nightrunner_Item_Ray_Return" base on hit: 1 entity, 2 block, 3 no hit
            # scoreboard players set @s Nm_testing_wand_active 1
        }
        block raycast{
            <%%
                let CheckQuality = 0.9;
                let ExecuteAsRaycast = 'execute as @e[dx=0,type=!#nightrunner:not_mob,tag=!raycasting,limit=1] positioned ~-';
                let RunNightrunnerWandFunction = ' run function nightrunner:items/tools/' + data.Folder + '/';
                // If hit entity
                if(data.HitType == 1)
                {
                    emit("# If hit entity");
                    // Add coordinates
                    ExecuteAsRaycast = ExecuteAsRaycast + CheckQuality +' ~-' + CheckQuality +' ~-' + CheckQuality +' if entity @s[dx=0] positioned ~' + CheckQuality +' ~' + CheckQuality +' ~' + CheckQuality;
                    if(data.MainSpell == "Damage")
                    {
                        emit(ExecuteAsRaycast + RunNightrunnerWandFunction + data.FunctionName + '/spell_damage');
                    }
                    else {
                        emit(ExecuteAsRaycast + RunNightrunnerWandFunction + data.FunctionName + '/hit_entity');
                    }
                    // Return if hit entity
                    emit(ExecuteAsRaycast +' run return 1');
                }
                // If hit block
                emit("# If hit block");
                if(data.HitType == 1)
                {
                    // Cloud particle
                    emit('execute unless block ~ ~ ~ #nightrunner:air run particle minecraft:cloud ~ ~ ~ 0 0 0 0.02 1 force');
                }
                else
                {
                   emit('execute unless block ~ ~ ~ #nightrunner:air' + RunNightrunnerWandFunction + data.FunctionName + '/hit_block');
                }
                emit('execute unless block ~ ~ ~ #nightrunner:air run return 2');
            %%>
            #
            execute if score $Nightrunner Nightrunner_Item_Ray_steps >= $Nightrunner Nightrunner_RangedSpellRange run block{
                particle minecraft:cloud ~ ~ ~ 0 0 0 0.02 1 force
            }
            execute if score $Nightrunner Nightrunner_Item_Ray_steps >= $Nightrunner Nightrunner_RangedSpellRange run return 3

            # particle effect
            execute if score $Nightrunner Nightrunner_Item_Ray_steps matches 15.. run block effect{
                # cooldown
                execute if score $NightrunnerCooldown Nightrunner_EffectCooldown matches 1.. run scoreboard players remove $NightrunnerCooldown Nightrunner_EffectCooldown 1
                execute if score $NightrunnerCooldown Nightrunner_EffectCooldown matches 1.. run return 1
                particle <%data.Particle%> ~ ~ ~ 0 0 0 0.02 1 force
                scoreboard players add $NightrunnerCooldown Nightrunner_EffectCooldown 8
            }

            # Update steps
            scoreboard players add $Nightrunner Nightrunner_Item_Ray_steps 1
            # Loop
            execute positioned ^ ^ ^0.25 rotated ~ ~ run function ./raycast
        }
        block give{
            give @s minecraft:warped_fungus_on_a_stick[custom_data={<%data.Namespace%>:true,wand:true},item_name='{"text":"<% data.Name %>","italic":false,"color":"<%data.Color%>"}',lore=['{"text":"<%data.Description%>"}','{"text":"Nightrunner","color":"aqua"}'],custom_model_data=<%data.CustomModelData%>] 1
        }
        block check{
            execute as @a if score @s Nightrunner_Used_Magic_tool matches 1 run block check_wand{
                # Selected item
                <%%
                    let MainFunctionName = 'function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/use';
                    if(data.MainSpell == "Damage")
                    {
                        MainFunctionName = 'execute at @s run function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + "/start_raycast";
                    }
                    else if(data.MainSpell == "Heal")
                    {
                        MainFunctionName = 'function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/spell_heal';
                    }
                    else if(data.MainSpell == "Raycast")
                    {
                        MainFunctionName = 'execute at @s run function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/start_raycast';
                    }
                    emit('execute if entity @s[nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",components:{"minecraft:custom_data":{' + data.Namespace + ':true,wand:true}}}}] run ' + MainFunctionName);

                    emit('# Offhand item');
                    let OffhandFunctionName = 'function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/use_secondary';
                    if(data.SecondarySpell == "Damage")
                    {
                        OffhandFunctionName = 'execute at @s run function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + "/start_raycast";
                    }
                    else if(data.SecondarySpell == "Heal")
                    {
                        OffhandFunctionName = 'function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/spell_heal';
                    }
                    else if(data.SecondarySpell == "Raycast")
                    {
                        OffhandFunctionName = 'execute at @s run function nightrunner:items/tools/' + data.Folder + '/' + data.FunctionName + '/start_raycast';
                    }
                    emit('execute if entity @s[nbt={Inventory:[{id:"minecraft:warped_fungus_on_a_stick",Slot:-106b,components:{"minecraft:custom_data":{' + data.Namespace + ':true,wand:true}}}]}] run ' + OffhandFunctionName);
                %%>
                # Reset
                execute if entity @s[nbt={SelectedItem:{id:"minecraft:warped_fungus_on_a_stick",components:{"minecraft:custom_data":{<%data.Namespace%>:true,wand:true}}}}] run scoreboard players reset @s Nightrunner_Used_Magic_tool
                # Reset
                execute if entity @s[nbt={Inventory:[{id:"minecraft:warped_fungus_on_a_stick",Slot:-106b,components:{"minecraft:custom_data":{<%data.Namespace%>:true,wand:true}}}]}] run scoreboard players reset @s Nightrunner_Used_Magic_tool
            }
        }
        block spell_damage{
            # Apply damage if the player has the wand in their hand
            execute if entity @a[limit=1,tag=raycasting,sort=nearest] run block apply_spell_damage{
                # check if entitiy is iron golem
                execute if entity @s[type=minecraft:iron_golem] run return run block{
                    damage @a[limit=1,tag=raycasting,sort=nearest] 2 minecraft:magic by @s
                    playsound minecraft:item.trident.riptide_2 player @a ~ ~ ~ 0.4 0.4
                }

                damage @s <%data.SpellDamage%> minecraft:magic by @a[limit=1,tag=raycasting,sort=nearest]
                playsound minecraft:entity.arrow.hit player @a ~ ~ ~ 0.4 0.4
                effect give @s slowness 1 1 true
            }
        }
        block spell_heal{
            effect give @s minecraft:instant_health 1 <%data.HealAmount%>
        }
    }
}